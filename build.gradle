/*
 * Copyright 2015-2018 the original author or authors.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import java.text.SimpleDateFormat

buildscript {
    repositories {
        jcenter()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'org.kt3k.gradle.plugin:coveralls-gradle-plugin:2.8.2'
        classpath 'nl.javadude.gradle.plugins:license-gradle-plugin:0.11.0'
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.0'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.17.0'
        classpath 'org.ajoberstar:gradle-git:1.7.2'
        classpath 'net.nemerosa:versioning:2.6.1'
        classpath 'org.kordamp.gradle:stats-gradle-plugin:0.2.2'
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'net.nemerosa.versioning'
apply plugin: 'com.github.kt3k.coveralls'
apply plugin: 'com.github.ben-manes.versions'

Date buildTimeAndDate = new Date()
ext {
    buildBy = System.properties['user.name']
    buildDate = new SimpleDateFormat('yyyy-MM-dd').format(buildTimeAndDate)
    buildTime = new SimpleDateFormat('HH:mm:ss.SSSZ').format(buildTimeAndDate)
    buildRevision = versioning.info.commit
    buildCreatedBy = "${System.properties['java.version']} (${System.properties['java.vendor']} ${System.properties['java.vm.version']})".toString()
}

apply from: 'gradle/publishing.gradle'
apply from: 'gradle/code-coverage.gradle'
apply from: 'gradle/code-quality.gradle'

idea {
    project {
        jdkName rootProject.sourceCompatibility
        languageLevel rootProject.sourceCompatibility
    }
}

repositories {
    jcenter()
}

if (JavaVersion.current().isJava8Compatible()) {
    tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
    }
}

tasks.withType(JavaCompile) {
    options.compilerArgs = ['-Xlint:all', '-Xlint:deprecation', '-Xlint:unchecked']
}

dependencies {
    compile "${project.group}:jsr377-api:$jsr377Version"
    compile "junit:junit:$junitVersion"
    compile "pl.pragmatists:JUnitParams:$junitParamsVersion"
    compile "org.hamcrest:java-hamcrest:$hamcrestVersion"
    compile "org.awaitility:awaitility:$awaitilityVersion"
    compile "org.assertj:assertj-core:$assertjVersion"
    testCompile "org.mockito:mockito-core:$mockitoVersion"
}

task sourcesJar(type: Jar) {
    group 'Build'
    description 'An archive of the source code'
    classifier 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    group 'Build'
    description 'An archive of the javadoc'
    classifier 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    sourcesJar
    javadocJar
}

javadoc {
    excludes = ['**/*.html', 'META-INF/**', '**/*.groovy']

    options.use         = true
    options.splitIndex  = true
    options.encoding    = 'UTF-8'
    options.author      = true
    options.version     = true
    options.windowTitle = "$project.name $version"
    options.docTitle    = "$project.name $version"
    options.footer      = project.javadocFooter
    options.links       = ['http://docs.oracle.com/javase/8/docs/api/']
}